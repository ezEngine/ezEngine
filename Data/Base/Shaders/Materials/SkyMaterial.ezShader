[PLATFORMS]
ALL

[PERMUTATIONS]

BLEND_MODE
RENDER_PASS

[MATERIALPARAMETER]

Permutation BLEND_MODE;

float MaskThreshold @Default(0.25);
float ExposureBias;
bool InverseTonemap;

TextureCube CubeMap;
Color BaseColor @Default(1.0, 1.0, 1.0);

[RENDERSTATE]

#define TWO_SIDED TRUE
#include <Shaders/Materials/MaterialState.h>

DepthWrite = false

[VERTEXSHADER]

#define CUSTOM_INTERPOLATOR float3 ViewDirection : TEXCOORD0;

#include <Shaders/Materials/SkyMaterialCB.h>
#include <Shaders/Materials/MaterialVertexShader.h>

VS_OUT main(VS_IN Input)
{
	ezPerInstanceData data = GetInstanceData(Input);
	float4x4 objectToWorld = TransformToMatrix(data.ObjectToWorld);
	
	VS_OUT Output;
	float2 ScreenPos = sign(Input.Position.xy);
	float4 dir = mul(ScreenToCameraMatrix, float4(ScreenPos.xy,1.0,1.0));
	dir.w = 0.0;

	Output.Position = float4(ScreenPos.xy, 1.0, 1.0);
	Output.ViewDirection = mul(transpose(objectToWorld), mul(CameraToWorldMatrix, dir)).xyz;

	return Output;
}

[PIXELSHADER]

#define CUSTOM_INTERPOLATOR float3 ViewDirection : TEXCOORD0;

#include <Shaders/Materials/SkyMaterialCB.h>
#include <Shaders/Materials/MaterialPixelShader.h>

TextureCube CubeMap;
SamplerState CubeMap_AutoSampler;


float3 GetNormal(PS_IN Input)
{
	return float3(0.0f, 0.0f, 1.0f);
}

float3 GetDiffuseColor(PS_IN Input)
{
	float3 color = CubeMap.Sample(CubeMap_AutoSampler, CubeMapDirection(Input.ViewDirection)).rgb;

	color *= BaseColor.rgb;
	float weight = exp2(ExposureBias);

	if (InverseTonemap)
	{
		float luminance = GetLuminance(color);
		weight *= rcp(max(1.0f - luminance, 0.001f));
	}

	color *= weight;

	return color;
}

float3 GetSpecularColor(PS_IN Input)
{
	return 0.0f;
}

float GetRoughness(PS_IN Input)
{
	return 1.0f;
}

float GetOpacity(PS_IN Input)
{
	float opacity = CubeMap.Sample(CubeMap_AutoSampler, CubeMapDirection(Input.ViewDirection)).a * BaseColor.a;

	#if BLEND_MODE == BLEND_MODE_MASKED
		return opacity - MaskThreshold;
	#else
		return opacity;
	#endif
}
