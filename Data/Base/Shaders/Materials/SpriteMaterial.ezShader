[PLATFORMS]
ALL

[PERMUTATIONS]

SHAPE_ICON
BLEND_MODE
RENDER_PASS
MSAA
TWO_SIDED = FALSE
FLIP_WINDING = FALSE
CAMERA_MODE
VERTEX_SHADER_RENDER_TARGET_ARRAY_INDEX
SHADING_MODE = SHADING_MODE_FULLBRIGHT

[RENDERSTATE]

#include <Shaders/Materials/MaterialState.h>
WireFrame = false

[SHADER]

#define USE_TEXCOORD0
#define USE_COLOR0

#if (RENDER_PASS == RENDER_PASS_EDITOR) || (RENDER_PASS == RENDER_PASS_PICKING) || (RENDER_PASS == RENDER_PASS_PICKING_WIREFRAME)
  #define USE_GAME_OBJECT_ID
#endif

#if defined(USE_GAME_OBJECT_ID)
  #define CUSTOM_INTERPOLATOR uint GameObjectID : GAME_OBJECT_ID;
#endif

[VERTEXSHADER]

#include <Shaders/Common/GlobalConstants.h>
#include <Shaders/Materials/MaterialInterpolator.h>
#include <Shaders/Materials/SpriteData.h>

StructuredBuffer<ezPerSpriteData> spriteData;

VS_OUT main(uint VertexID : SV_VertexID, uint InstanceID : SV_InstanceID)
{
#if CAMERA_MODE == CAMERA_MODE_STEREO
  s_ActiveCameraEyeIndex = InstanceID % 2;
#endif

  float2 vertexPositions[6] =
  {
    float2(0.0, 0.0),
    float2(0.0, 1.0),
    float2(1.0, 1.0),
    float2(0.0, 0.0),
    float2(1.0, 1.0),
    float2(1.0, 0.0),
  };

  VS_OUT Output;
  #if VERTEX_SHADER_RENDER_TARGET_ARRAY_INDEX == TRUE && CAMERA_MODE == CAMERA_MODE_STEREO    
    Output.RenderTargetArrayIndex = InstanceID;
  #else
    Output.InstanceID = InstanceID;
  #endif

  uint index = VertexID / 6;
  uint vertexIndex = VertexID % 6;

  float2 texCoords = vertexPositions[vertexIndex];
  float2 vertexPosition = (texCoords - 0.5);

  texCoords.y = 1.0 - texCoords.y;

  ezPerSpriteData data = spriteData[index];
  float3 worldPosition = data.WorldSpacePosition;
  float4 screenPosition = mul(GetWorldToScreenMatrix(), float4(worldPosition, 1.0));

  float distance = screenPosition.w;
  float2 scale = mul(GetCameraToScreenMatrix(), float4(1, 1, 1, 1)).xy;

  scale *= data.Size;

  if (data.MaxScreenSize > 0.0)
  {
    float2 maxScreenSize = (data.MaxScreenSize * distance * 2.0) * ViewportSize.zw;
    scale = min(scale, maxScreenSize);
  }

  scale.x *= data.AspectRatio;
  screenPosition.xy += vertexPosition * scale;

  float2 texCoordScale = RG16FToFloat2(data.TexCoordScale);
  float2 texCoordOffset = RG16FToFloat2(data.TexCoordOffset);

  Output.Position = screenPosition;
  Output.TexCoord0 = texCoords * texCoordScale + texCoordOffset;
  Output.Color0 = RGBA16FToFloat4(data.ColorRG, data.ColorBA);

  #if defined(USE_GAME_OBJECT_ID)
    Output.GameObjectID = data.GameObjectID;
  #endif

  return Output;
}

[GEOMETRYSHADER]

#define CopyCustomInterpolators(output, input) output.GameObjectID = input.GameObjectID;

#include <Shaders/Materials/MaterialStereoGeometryShader.h>

[PIXELSHADER]

#if BLEND_MODE == BLEND_MODE_MASKED && MSAA
  #define USE_ALPHA_TEST_SUPER_SAMPLING
#endif

#include <Shaders/Materials/MaterialHelper.h>

struct PS_OUT
{
  #if RENDER_PASS != RENDER_PASS_DEPTH_ONLY
    float4 Color : SV_Target;
  #endif

  #if defined(USE_ALPHA_TEST_SUPER_SAMPLING)
    uint Coverage : SV_Coverage;
  #endif
};

Texture2D SpriteTexture;
SamplerState SpriteTexture_AutoSampler;

float GetOpacity()
{
  float opacity = SpriteTexture.Sample(SpriteTexture_AutoSampler, G.Input.TexCoord0.xy).a * G.Input.Color0.a;

  #if BLEND_MODE == BLEND_MODE_MASKED
    return opacity - 0.5;
  #else
    return opacity;
  #endif
}

PS_OUT main(PS_IN Input)
{
#if CAMERA_MODE == CAMERA_MODE_STEREO
  s_ActiveCameraEyeIndex = Input.RenderTargetArrayIndex;
#endif

  G.Input = Input;

  uint coverage = CalculateCoverage();
  if (coverage == 0)
  {
    discard;
  }

  float4 color = SpriteTexture.Sample(SpriteTexture_AutoSampler, Input.TexCoord0.xy);

#if SHAPE_ICON
  float3 tintedColor = saturate(color.rgb * Input.Color0.rgb * 2.0);
  float factor = saturate(GetLuminance(color.rgb) * 3.0 - 2.0);
  color.rgb = lerp(tintedColor, 1.0, factor);  
#else
  color *= Input.Color0;
#endif

  PS_OUT Output;

  #if (RENDER_PASS == RENDER_PASS_PICKING) || (RENDER_PASS == RENDER_PASS_PICKING_WIREFRAME)
    Output.Color = RGBA8ToFloat4(Input.GameObjectID);
  #elif RENDER_PASS == RENDER_PASS_EDITOR
    if (RenderPass == EDITOR_RENDER_PASS_STATIC_VS_DYNAMIC)
    {
      Output.Color = ColorizeGameObjectId(Input.GameObjectID);
    }
    else
    {
      Output.Color = color;
    }
  #elif RENDER_PASS != RENDER_PASS_DEPTH_ONLY
    Output.Color = color;
  #endif

  #if defined(USE_ALPHA_TEST_SUPER_SAMPLING)
    Output.Coverage = coverage;
  #endif

  #if RENDER_PASS != RENDER_PASS_DEPTH_ONLY && BLEND_MODE == BLEND_MODE_MASKED
      Output.Color.a = 1.0f;
  #endif
  return Output;
}
