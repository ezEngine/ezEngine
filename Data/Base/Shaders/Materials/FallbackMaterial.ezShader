[PLATFORMS]
ALL

[PERMUTATIONS]

BLEND_MODE
RENDER_PASS
TOPOLOGY
SHADING_MODE
TWO_SIDED
FLIP_WINDING
WRITE_DEPTH
APPLY_SSAO_TO_DIRECT_LIGHTING

[RENDERSTATE]

#include <Shaders/Materials/MaterialState.h>

[VERTEXSHADER]

#include <Shaders/Materials/DefaultMaterialCB.h>
#include <Shaders/Materials/MaterialVertexShader.h>

VS_OUT main(VS_IN Input)
{
  return FillVertexData(Input);
}

[GEOMETRYSHADER]

#if TOPOLOGY == TOPOLOGY_TRIANGLES

struct GS_IN
{
  float4 Position : SV_Position;
  float3 WorldPos : WORLDPOS;
  uint InstanceOffset : INSTANCEOFFSET;
};

struct GS_OUT
{
  float4 Position : SV_Position;
  float3 WorldPos : WORLDPOS;
  float3 Normal : NORMAL;
  uint InstanceOffset : INSTANCEOFFSET;
};

[maxvertexcount(3)]
void main( triangle GS_IN input[3], inout TriangleStream<GS_OUT> outputStream )
{
  float3 edge1 = input[1].WorldPos - input[0].WorldPos;
  float3 edge2 = input[2].WorldPos - input[0].WorldPos;
  float3 normal = normalize(cross(edge1, edge2));

  GS_OUT output;

  for (uint i = 0; i < 3; ++i)
  {
    output.Position = input[i].Position;
    output.WorldPos = input[i].WorldPos;
    output.Normal = normal;
    output.InstanceOffset = input[i].InstanceOffset;

    outputStream.Append(output);
  }

  outputStream.RestartStrip();
}

#endif

[PIXELSHADER]

#if TOPOLOGY == TOPOLOGY_TRIANGLES
  #define USE_NORMAL
#endif

#include <Shaders/Materials/DefaultMaterialCB.h>
#include <Shaders/Materials/MaterialPixelShader.h>

Texture2D BaseTexture;
SamplerState BaseTexture_AutoSampler;

float3 GetNormal(PS_IN Input)
{
  #if TOPOLOGY == TOPOLOGY_TRIANGLES
    return Input.Normal;
  #else
    return float3(0.0f, 0.0f, 1.0f);
  #endif
}

float3 GetDiffuseColor(PS_IN Input)
{
  #if TOPOLOGY == TOPOLOGY_TRIANGLES
    return SampleTexture3Way(BaseTexture, BaseTexture_AutoSampler, Input.Normal, Input.WorldPosition, 1.0).rgb;
  #else
    return float3(0.5, 0.5, 0.5);
  #endif
}

float3 GetSpecularColor(PS_IN Input)
{
  return 0.04f;
}

float GetRoughness(PS_IN Input)
{
  return 1.0f;
}

float GetOpacity(PS_IN Input)
{
  return 1.0f;
}
