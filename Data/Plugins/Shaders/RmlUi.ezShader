[PLATFORMS]
ALL

[PERMUTATIONS]

RMLUI_MODE

[RENDERSTATE]

BlendingEnabled0 = true
DestBlend0 = Blend_InvSrcAlpha
SourceBlend0 = Blend_One
ScissorTest = true
DepthTest = false
DepthWrite = false
CullMode = CullMode_None
StencilReadMask = 1
StencilWriteMask = 1

#if RMLUI_MODE == RMLUI_MODE_STENCIL_TEST

StencilTest = true
FrontFaceStencilFunc = CompareFunc_Equal

#elif RMLUI_MODE == RMLUI_MODE_STENCIL_SET

StencilTest = true
FrontFaceStencilFunc = CompareFunc_Always
FrontFacePassOp = StencilOp_Replace
WriteMask0 = 0

#endif

[VERTEXSHADER]

#include "RmlUiConstants.h"

#if RMLUI_MODE != RMLUI_MODE_STENCIL_SET
  #define USE_COLOR0
  #define USE_TEXCOORD0

  #include <Shaders/Materials/MaterialInterpolator.h>
#endif

float4 GetScreenPosition(float4 inputPos)
{
  float3 screenPosition = mul(UiTransform, inputPos).xyz;
  screenPosition.xy = (screenPosition.xy * ViewportSize.zw) * float2(2.0, -2.0) + float2(-1.0, 1.0);
  return float4(screenPosition, 1.0f);
}

#if RMLUI_MODE == RMLUI_MODE_STENCIL_SET
  float4 main(uint VertexID : SV_VertexID) : SV_Position
  {
    float4 inputPos = QuadVertexPos[VertexID];
    return GetScreenPosition(inputPos);
  }
#else
  VS_OUT main(VS_IN Input)
  {
    VS_OUT RetVal;
    
    float4 inputPos = float4(Input.Position, 1);
    inputPos.xy += UiTranslation.xy;
    RetVal.Position = GetScreenPosition(inputPos);

    RetVal.TexCoord0 = Input.TexCoord0;
    RetVal.Color0 = Input.Color0;

    return RetVal;
  }
#endif

[PIXELSHADER]

#if RMLUI_MODE == RMLUI_MODE_STENCIL_SET
  void main()
  {
  }
#else
  #define USE_COLOR0
  #define USE_TEXCOORD0

  #include <Shaders/Materials/MaterialInterpolator.h>

  Texture2D BaseTexture;
  SamplerState BaseTexture_AutoSampler;

  float4 main(PS_IN Input) : SV_Target
  {
    return BaseTexture.Sample(BaseTexture_AutoSampler, Input.TexCoord0.xy) * Input.Color0;
  }
#endif